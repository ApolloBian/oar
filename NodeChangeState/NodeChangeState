#!/usr/bin/perl
# $Id: NodeChangeState,v 1.11 2005/10/03 14:36:36 capitn Exp $
#Almighty module which changes node state

use English;
use oar_iolib;
use Data::Dumper;
use oar_Judas qw(oar_debug oar_warn oar_error);
use IO::Socket::INET;
use strict;

my $exitCode = 0;

my $base = iolib::connect();

iolib::lock_table($base,["resources","assigned_resources","jobs","resource_state_logs","event_logs","events_log_hostnames"]);

# Check event logs
my @events_to_check = iolib::get_to_check_events($base);
foreach my $i (@events_to_check){
    oar_debug("[NodeChangeState] Check event for the job $i->{idJob} with type $i->{type}\n");
    if (($i->{type} eq "PING_CHECKER_NODE_SUSPECTED") ||
        ($i->{type} eq "PROLOGUE_ERROR") ||
        ($i->{type} eq "EPILOGUE_ERROR") ||
        ($i->{type} eq "CAN_NOT_WRITE_NODE_FILE") ||
        ($i->{type} eq "CAN_NOT_WRITE_PID_FILE") ||
        ($i->{type} eq "USER_SHELL") ||
        ($i->{type} eq "EXTERMINATE_JOB") ||
        ($i->{type} eq "EXIT_VALUE_OAREXEC")
       ){
        my @hosts;
        my $finaudTag;
        # Restrict Suspected state to the first node (node really connected with OAR) for some event types
        if (($i->{type} eq "PING_CHECKER_NODE_SUSPECTED")){
            @hosts = iolib::get_hostname_event($base,$i->{event_id});
            $finaudTag = "YES";
        }else{
            @hosts = iolib::get_job_host_log($base,$i->{assigned_moldable_job});
            $finaudTag = "NO";
            if (($i->{type} ne "EXTERMINATE_JOB")){
                @hosts = ($hosts[0]);
            }
        }

        foreach my $j (@hosts){
            my @free_resources = iolib::get_current_free_resources_of_node($base, $j);
            if ($#free_resources < 0){
                oar_warn("[NodeChangeState] There was an error ($i->{type}) on the node $j SO we are suspecting resource(s) : @free_resources\n");
                foreach my $r (@free_resources){
                    iolib::set_node_state($base,$j,"Suspected",$finaudTag);
                    $exitCode = 1;
                }
            }else{
                oar_warn("[NodeChangeState] There was an error ($i->{type}) on the node $j BUT we cannot Suspect this node because there is at least one other job on it\n");
            }
        }
    }
    iolib::check_event($base, $i->{type}, $i->{job_id});
}


# Treate nextState field
my %resources_to_change = iolib::get_resources_change_state($base);

#A Term command must be added in the Almighty
oar_debug("[NodeChangeState] number of resources to change state = ".keys(%resources_to_change)."\n");
if (keys(%resources_to_change) > 0){
    $exitCode = 1;
}

foreach my $i (keys(%resources_to_change)){
    my $resource_info = iolib::get_resource_info($base,$i);
    if ($resource_info->{state} ne $resources_to_change{$i}){
        if ($resource_info->{next_finaud_decision} eq "YES"){
            oar_warn("[NodeChangeState] Finaud is automatically changing the state of the resource $i into $resources_to_change{$i}\n");
        }else{
            oar_warn("[NodeChangeState] $i --> $resources_to_change{$i}\n");
        }

        iolib::set_resource_state($base,$i,$resources_to_change{$i},$resource_info->{next_finaud_decision});
        iolib::set_resource_nextState($base,$i,'UnChanged');

        if (($resources_to_change{$i} eq 'Dead') || ($resources_to_change{$i} eq 'Absent')){
            oar_debug("[NodeChangeState] Check jobs to delete on $i :\n");
            my @jobs = iolib::get_resource_job($base,$i);
            foreach my $j (@jobs){
                oar_debug("[NodeChangeState]\tThe job $j is fragging.\n");
                iolib::frag_job($base,$j);
                # A Leon must be run
                $exitCode = 2;
            }
            oar_debug("[NodeChangeState] Check done\n");
        }
    }else{
        oar_warn("[NodeChangeState] $i is already in the $resources_to_change{$i} state\n");
        iolib::set_resource_nextState($base,$i,'UnChanged');
    }
}
iolib::unlock_table($base);
iolib::disconnect($base);

exit($exitCode);
