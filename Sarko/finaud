#!/usr/bin/perl
# $Id: finaud,v 1.4 2005/05/04 13:03:36 capitn Exp $
# This program aims to change node state automatically when they are inaccesible or they become alive

use oar_iolib;
use oar_Judas qw(oar_debug oar_warn oar_error);
use ping_checker qw(test_hosts);
use Data::Dumper;
use strict;
use IO::Socket::INET;

oar_debug("[finaud] Finaud started\n");

oar_debug("[finaud] Check Alive and Suspected nodes\n");
my $base = iolib::connect();

my $Occupied_resources_vector;
for my $r (iolib::get_current_assigned_resources($base)){
    vec($Occupied_resources_vector,$r,1) = 1;
}

my @Resource_list;
push(@Resource_list, iolib::get_resources_in_state($base,"Alive"));
push(@Resource_list, iolib::get_resources_in_state($base,"Suspected"));

my %Node_list;

foreach my $i (@Resource_list){
    #Do not check occupied resources
    if (!vec($Occupied_resources_vector, $i->{resourceId}, 1)){
        push(@{$Node_list{$i->{networkAdress}}}, $i->{resourceId});
    }
}

my @Nodes_to_check = keys(%Node_list);
oar_debug("[finaud] Testing resource(s) on : @Nodes_to_check\n");

# Call the right program to test each nodes
my @badNodes = test_hosts(@Nodes_to_check);

my %badNodeHash;
foreach my $i (@badNodes){
    $badNodeHash{$i} = 0;
}

#Make the decisions
my $returnValue = 0;
foreach my $i (@Resource_list){
    if (defined($badNodeHash{$i->{networkAdress}}) and ($i->{state} eq "Alive")){
        iolib::set_resource_nextState($base,$i->{resourceId},"Suspected");
        iolib::update_resource_nextFinaudDecision($base,$i->{resourceId},"YES");
        $returnValue = 1;
        oar_debug("[finaud] Set the next state of $i->{resourceId}, $i->{networkAdress} to Suspected\n");
    }elsif (!defined($badNodeHash{$i->{networkAdress}}) and ($i->{state} eq
    "Suspected")){
        iolib::set_resource_nextState($base,$i->{resourceId},"Alive");
        iolib::update_resource_nextFinaudDecision($base,$i->{resourceId},"YES");
        $returnValue = 1;
        oar_debug("[finaud] Set the next state of $i->{resourceId}, $i->{networkAdress} to Alive\n");
    }
}

iolib::disconnect($base);

oar_debug("[finaud] Finaud ended : $returnValue\n");

exit($returnValue);
