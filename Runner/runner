#!/usr/bin/perl
#Almighty module : launch job bipbips

use strict;
use DBI();
use Data::Dumper;
use oar_iolib;
use oar_Judas qw(oar_debug oar_warn oar_error);
use oar_conflib qw(init_conf dump_conf get_conf is_conf);
use oar_Tools;

init_conf("oar.conf");
my $binpath;
if (defined($ENV{OARDIR})){
    $binpath = $ENV{OARDIR}."/";
}else{
    oar_error("[Runner] OARDIR env variable must be defined\n");
    exit(3);
}

my $batch = $binpath."bipbip";
my $inter = $binpath."bipbip";

my $exitCode = 0;

sub answer ($$$){
    my $jobid = shift;
    my $info = shift;
    my $message = shift;

    my $error = 0;
    my ($addr,$port) = split(/:/,$info);
    oar_debug("[Runner] oarsub addr:port = $addr:$port info = $info\n");
    if(!defined(oar_Tools::notifyTCPSocket($addr,$port,"$message"))){
        oar_debug("[Runner] Notification done\n");
    }else{
        oar_error("[Runner] Cannot open connection to oarsub client for job $jobid !\n");
        $error = 1;
    }
    return($error);
}

my $base = iolib::connect();

# for toError jobs
my @jobErrorList = iolib::get_jobs_in_state($base,"toError");
foreach  my $j (@jobErrorList){
    oar_debug("[Runner] Treate job $j->{idJob} in toError state\n");
    if (($j->{jobType} eq "INTERACTIVE") || (($j->{jobType} eq "PASSIVE") && $j->{reservation} eq "Scheduled")){
        oar_debug("[Runner] Notify oarsub for an INTERACTIVE job (num:$j->{idJob}) in error; jobInfo=$j->{infoType}\n");
        answer($j->{idJob},$j->{infoType},"BAD JOB");
    }
    oar_debug("[Runner] Set job $j->{idJob} to state Error\n");
    iolib::set_job_state($base, $j->{idJob}, "Error");
}

# for toAckReservation jobs
my @jobResaToAck = iolib::get_jobs_in_state($base,"toAckReservation");
for my $j (@jobResaToAck){
    oar_debug("[Runner] Treate job $j->{idJob} in toAckReservation state\n");
    if (answer($j->{idJob},$j->{infoType},"GOOD RESERVATION") == 1){
        oar_warn("[Runner] Frag job $j->{idJob}), I cannot notify oarsub for the reservation\n");
        iolib::add_new_event($base,"CAN_NOT_NOTIFY_OARSUB",$j->{idJob},"[runner] Can not notify oarsub for the job $j->{idJob}");
        iolib::frag_job($base,$j->{idJob});
        $exitCode = 1;
    }else{
        oar_debug("[Runner] Notify oarsub for a RESERVATION (idJob=$j->{idJob}) --> OK; jobInfo=$j->{infoType}\n");
        iolib::set_job_state($base, $j->{idJob}, "Waiting");
        # Test if we must notify Almighty to Launch scheduler for the reservation
        if ($exitCode == 0){
            if (iolib::sql_to_local($j->{startTime}) - 1 <= iolib::sql_to_local(iolib::get_date($base))){
                $exitCode = 2;
            }
        }
    }
}

# for toLaunch jobs
my @joblist = iolib::get_jobs_in_state($base,"toLaunch");

foreach my $job (@joblist) {
    my $jobid = $job->{idJob};
    my $jobtype = $job->{jobType};
    my $jobinfo = $job->{infoType};
    #my $is_desktop_computing = iolib::is_job_desktopComputing($base,$jobid);
    #oar_debug("[Runner] is_desktop_computing=$is_desktop_computing\n");
    #if ($is_desktop_computing) {
    #    oar_debug("[Runner] Desktop computing job, I don't handle it !\n");
    #} else {
        iolib::set_job_state($base,$jobid,"Launching");
        my $cmdtofork = "";
        if($jobtype eq "INTERACTIVE"){
            oar_debug("[Runner] Interactive request nothing to do Answer to the client Qsub -I\n");
            # repondre au client sur jobinfo
            if (answer($jobid,$jobinfo,"GOOD JOB") == 1){
                oar_warn("[Runner] Frag job $jobid, I cannot notify oarsub\n");
                iolib::frag_job($base,$jobid);
                $exitCode = 1;
            }
        }else{
            $cmdtofork = $batch;
            oar_debug("[Runner] try to execute $cmdtofork $jobid \n");
            my $res = oar_Tools::fork_no_wait("$cmdtofork $jobid ");
            oar_debug("[Runner] Child PID = $res\n");
        }
    #}
}


iolib::disconnect($base);

exit($exitCode);
